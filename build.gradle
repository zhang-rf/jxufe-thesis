buildscript {
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE"
    }
}

allprojects {
    group "cn.jxufe"
    version "1.0-RELEASE"
}

subprojects {
    apply plugin: "java"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        mavenCentral()
    }
}

configure(subprojects.findAll { it.name.matches("thesis-app\\d+") }) {
    apply plugin: "org.springframework.boot"

    dependencies {
        compile "org.springframework.boot:spring-boot-starter"
        testCompile "org.springframework.boot:spring-boot-starter-test"

        compile("com.alibaba:dubbo:2.5.3") {
            exclude module: "spring"
        }
        compile("com.github.sgroschupf:zkclient:0.1") {
            exclude module: "jmxtools"
            exclude module: "jmxri"
            exclude module: "jms"
        }
    }
}

project("thesis-app1") {
    dependencies {
        compile project(":thesis-app1-dubbo")
    }
}

project("thesis-app2") {
    dependencies {
        compile project(":thesis-app2-dubbo")
    }
}

project("thesis-app3") {
    dependencies {
        compile project(":thesis-app1-dubbo")
        compile project(":thesis-app2-dubbo")
    }
}
